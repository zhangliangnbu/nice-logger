apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28



    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
//    implementation 'com.liang.android:nicelogger:0.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}

/**
 * 本地生成构件文件
 */
// 定义pom文件参数
def groupIdDefined = "com.liang.android"
def artifactIdDefined = "nicelogger"
def versionDefined = "0.0.2"

// 重命名生成的arr文件
android.libraryVariants.all { variant ->
    variant.outputs.all { output ->
        if (outputFile != null && outputFileName.endsWith('.aar')) {
            outputFileName = "${artifactIdDefined}-${versionDefined}.aar"
        }
    }
}

// 生成Java源码文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    baseName = "${artifactIdDefined}-${versionDefined}"
    classifier = 'sources'
}

// 生成Javadoc源码文件
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName = "${artifactIdDefined}-${versionDefined}"
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//artifacts {
//    archives sourcesJar
//    archives javadocJar
//}

// 生成pom文件
apply plugin: 'maven'

task createPom {
    doLast {
        pom {
            project {
                groupId "${groupIdDefined}"
                artifactId "${artifactIdDefined}"
                version "${versionDefined}"
                packaging "aar"
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }.writeTo("$libsDir/${artifactIdDefined}-${versionDefined}.pom")
    }
}
